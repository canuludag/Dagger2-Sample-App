apply plugin: 'com.android.application'

def keyPropertiesFile = rootProject.file("key.properties")
def keyProperties = new Properties()
keyProperties.load(new FileInputStream(keyPropertiesFile))

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.uludag.can.dagger2_sample_app"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {

        debug {
            buildConfigField "String", "BASE_URL", "\"https://newsapi.org/v2/\""
            minifyEnabled false
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"

            manifestPlaceholders = [
                    appName  : rootProject.ext.APP_NAME_DEBUG,
                    appIcon  : "@mipmap/ic_launcher_debug",
                    roundIcon: "@mipmap/ic_launcher_debug_round"
            ]
        }

        release {
            buildConfigField "String", "BASE_URL", "\"https://newsapi.org/v2/\""
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            manifestPlaceholders = [
                    appName  : rootProject.ext.APP_NAME,
                    appIcon  : "@mipmap/ic_launcher",
                    roundIcon: "@mipmap/ic_launcher_round"
            ]
        }
    }

    buildTypes.each {
        it.buildConfigField 'String', 'NEWS_API_KEY', keyProperties['api_key']
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // Support Library
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"


    // Dagger 2 Dependency Injection
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"

    // ButterKnife
    implementation "com.jakewharton:butterknife:$butterKnifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterKnifeVersion"

    // Networking
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"

    //HTTP Logging
    implementation "com.squareup.okhttp3:logging-interceptor:$loggingInterceptorVersion"

    // Image Loading
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:$glideVersion"

    // Object serialization
    implementation "com.google.code.gson:gson:$gsonVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    // Test
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "com.android.support.test:runner:$testRunnerVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espressoTestVersion"
}
